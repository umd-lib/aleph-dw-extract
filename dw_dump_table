#!/bin/csh -f

# dw_dump_table
#
# Script to dump specific table
#
# Environment variables:
#   dir   working directory
#   start first timestamp to extract
#   end   last timestamp to extract
#
# Revision History
#
#   2018/09/20: Hans
#     - first cut
#
set start = `date +"%Y%m%d_%H%M%S"`

if ($#argv != 3) then
    echo "Usage: $0 lib table suffix"
    exit
endif

set lib = $argv[1]
set pass = `get_ora_passwd $lib`
set table = $argv[2]
set suffix = $argv[3]

echo "`date` in dw_dump_table $lib $table"

set fileroot = $dir/${lib}_${table}
set flagfile = ${fileroot}_flags

set table_u = `echo $table | tr '[a-z]' '[A-Z]'`
# echo $table_u
grep " $table_u " $dir/${lib}_z800_${suffix}_b | grep -v ^Z > $flagfile
if (-s $flagfile) then
    if ($table_u == Z00) then
	mv $flagfile ${flagfile}.raw
	dw_clean_flags_z00 $lib $table ${flagfile}.raw $flagfile
	# add headers to field_data files
	set NOW = `date +"%Y-%m-%d %T"`
	printf "H\tDW Extract Job Number(work on this)\t$NOW\t$dir/${lib}_z800_${suffix}_b\n" > ${fileroot}_field_data
	printf "z00_doc_number\tz30_marc_rec_field_cd\tUNUSED\tz00_marc_rec_field_txt\n" >> ${fileroot}_field_data
	cut -c23- $flagfile | doc-extract -l $lib -d >> ${fileroot}_field_data
	dw_table_extract_z00 $lib $table $flagfile ${fileroot}_full_data
    endif
    dw_table_extract.py $lib $table $flagfile ${fileroot}_data $suffix
else
    cp /dev/null ${fileroot}_data
endif
